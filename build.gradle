plugins {
    id 'org.springframework.boot' version '3.3.2'
    id 'io.spring.dependency-management' version '1.1.5'
    id 'java-library'
    id 'jacoco'
}

ext {
    springCloudVersion = "2023.0.3"
}

group = 'org.sopt'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '21'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}


repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-security'

    // OpenFeign Client
    implementation "io.github.openfeign:feign-okhttp"
    implementation "io.github.openfeign:feign-jackson"
    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'


    // swagger
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.6.0'

    // sentry
    implementation 'io.sentry:sentry-spring-boot-starter:6.17.0'
    implementation 'io.sentry:sentry-logback:6.17.0'

    // QueryDSL
    implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
    annotationProcessor "com.querydsl:querydsl-apt:5.0.0:jakarta"  // 버전 명시
    annotationProcessor "jakarta.annotation:jakarta.annotation-api"
    annotationProcessor "jakarta.persistence:jakarta.persistence-api"

    // lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'

    //postgres
    runtimeOnly 'org.postgresql:postgresql'

    // mapper
    implementation 'org.mapstruct:mapstruct:1.5.3.Final'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.3.Final'

    // jwt
    implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
    implementation 'io.jsonwebtoken:jjwt-impl:0.11.5'
    implementation 'io.jsonwebtoken:jjwt-jackson:0.11.5'

    // DB
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'


    // redis
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'

    // ImmutableMap
    implementation 'com.google.guava:guava:32.0.1-jre'

    //hibernate-type52 추가 array type을 위해
    implementation 'io.hypersistence:hypersistence-utils-hibernate-63:3.8.2'
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    // aws-s3
    implementation 'software.amazon.awssdk:s3'

    // retry
    implementation 'org.springframework.retry:spring-retry:2.0.6'

    // jwt
    implementation 'com.nimbusds:nimbus-jose-jwt:9.37.3'
    implementation 'com.github.ben-manes.caffeine:caffeine:3.1.8'

    // test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    implementation 'com.google.code.gson:gson:2.10.1'
    testImplementation 'org.mockito:mockito-core:5.5.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.5.0'
    implementation 'net.bytebuddy:byte-buddy:1.14.15'
    implementation 'org.objenesis:objenesis:3.4'

    // slack
    implementation 'com.slack.api:slack-api-client:1.30.0'

    // AWS Lambda Dependencies for JAR deployment
    implementation 'com.amazonaws.serverless:aws-serverless-java-container-springboot3:2.1.5'
    implementation 'com.amazonaws:aws-lambda-java-core:1.4.0'
    implementation 'com.amazonaws:aws-lambda-java-events:3.16.1'

}
tasks.named('test') {
    useJUnitPlatform()
}

test {
    exclude '**/*RepositoryTest.*'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:$springCloudVersion"
        mavenBom "software.amazon.awssdk:bom:2.20.0"
    }
}

// Lambda ZIP 빌드 설정
task lambdaJar(type: Zip) {
    dependsOn bootJar
    archiveClassifier = 'lambda'
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    zip64 = true  // 대용량 ZIP 파일 지원

    // lib 디렉토리 구조로 패키징
    into('lib') {
        from(jar)
        from(configurations.runtimeClasspath) {
            // Lambda에서 불필요한 파일 제외
            exclude "org/apache/tomcat/embed/**"
            exclude "META-INF/*.SF"
            exclude "META-INF/*.DSA"
            exclude "META-INF/*.RSA"
            exclude "META-INF/MANIFEST.MF"
            exclude "**/module-info.class"
        }
    }
}

// Lambda 빌드를 기본 빌드에 포함
build.dependsOn lambdaJar

// JAR 설정
jar {
    enabled = true
    archiveClassifier = ''
}

bootJar {
    enabled = true
}